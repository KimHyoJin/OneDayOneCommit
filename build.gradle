plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "com.moowork.node" version "1.3.1"
}

group = 'com.hyojin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


def swagger_version = '3.0.0-SNAPSHOT'


repositories {
    mavenCentral()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'

    implementation "io.springfox:springfox-swagger2:${swagger_version}"
    implementation "io.springfox:springfox-swagger-ui:${swagger_version}"
    implementation "io.springfox:springfox-spring-webflux:${swagger_version}"
}

test {
    useJUnitPlatform()
}

def webappDir = "$projectDir/front-end"

task yarnInstall(type: YarnTask) {
    workingDir = file("${webappDir}")
    args = ["install"]
}


task yarnBuild(type: YarnTask) {
    workingDir = file("${webappDir}")
    args = ['build']
}

task copyFrontEnd(type: Copy) {
    from 'front-end/build'
    into 'build/resources/main/static/.'
}

task cleanFrontEnd(type: Delete) {
    delete "$projectDir/front-end/build", "$projectDir/front-end/node_modules"
}

yarnBuild.dependsOn yarnInstall
copyFrontEnd.dependsOn yarnBuild
processResources.dependsOn copyFrontEnd
clean.dependsOn cleanFrontEnd